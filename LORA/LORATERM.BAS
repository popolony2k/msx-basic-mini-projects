10 '/**
20 '  * LORA COMMUNICATION TERMINAL
30 '  *
40 '  * CopyLeft (c) by PopolonY2k 2023
50 '  */
60 SCREEN 0
70 RATE = 9600
80 PRINT "Waiting for data"
90 CALL COMINI ("8N1NNNAN", RATE)
100 OPEN "COM:" AS #1
110 CALL COM(, GOSUB 330)
120 CALL COMON
130 '
140 ' *** USER INPUT ***
150 '
160 C$=""
170 A$=INKEY$
180 IF A$="" THEN 170 ELSE IF A$=CHR$(13) THEN 220 ELSE C$=C$+A$:PRINTA$;:GOTO170
190 '
200 ' *** SEND DATA ***
210 '
220 L=LEN(C$)
230 IF L MOD 2=1 OR L=0 THEN PRINT "INVALID MESSAGE" : GOTO 160
240 D$="":FOR I=1 TO L STEP 2
250 D$=D$+CHR$(VAL("&H"+MID$(C$,I,2)))
260 NEXTI
270 PRINT " SENT =>" + C$ + CHR$(13)
280 PRINT #1,D$
290 GOTO 160
300 '
310 ' *** DATA RECEIVE ROUTINE ***
320 '
330 B$="":B$=INPUT$(1,#1)
340 IF ASC(B$)=&HFF THEN B$=INPUT$(2,#1):IF B$=CHR$(&HFF)+CHR$(&HFF) THEN PRINT "WRONG FORMAT RESPONSE":RETURN 160
350 B$=INPUT$(2,#1)
360 S=ASC(MID$(B$,2,1))
370 B$=INPUT$(S,#1)
380 PRINT "RECV => ";B$
390 L=LEN(B$)
400 PRINT "LEN = ", L
410 FOR I=1 TO L
420 PRINTHEX$(ASC(MID$(B$,I,1)))+" ";
430 NEXTI
440 PRINT CHR$(13)
450 C$=""
460 RETURN 160
470 PRINT "End of Transmission"
480 CLOSE #1
